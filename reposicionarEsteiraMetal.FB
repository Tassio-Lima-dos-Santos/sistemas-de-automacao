FUNCTION_BLOCK reposicionarEsteiraMetal
VAR_INPUT
    start   : BOOL;  // pulso de start
END_VAR

VAR_OUTPUT
    done : BOOL;      // TRUE quando chegou
END_VAR

VAR
    empCNC : FB_MoveTo;
    posicionarEstMetal : posicionarEsteiraMetal;
	trigStart : R_TRIG;
	trigSensor : R_TRIG;
    state : INT := 0;

END_VAR

trigStart(CLK:=start);

CASE state OF

    0: // Idle
        IF trigStart.Q THEN
            state := 10;
			done := FALSE;
        END_IF

    10: // Move to conveyor position
        empCNC(targetX := constante.XconveyorMetal, targetY := constante.YconveyorMetal, targetZ := constante.ZconveyorMetal, start := TRUE);
        IF empCNC.done THEN
            empCNC(start := FALSE);
            state := 20;
        END_IF

    20: // Turn on conveyor
        FIO.o_EMETAL1 := TRUE;
        FIO.o_EMETAL2 := TRUE;
        FIO.o_EMETAL3 := TRUE;
        state := 30;


    30: // Wait for trigger
        trigSensor(CLK:=FIO.i_SPP);
        IF trigSensor.Q THEN
            FIO.o_EMETAL1 := FALSE;
            FIO.o_EMETAL2 := FALSE;
            FIO.o_EMETAL3 := FALSE;
            state := 40;
        END_IF

    40: // call reposicionar FB
      	posicionarEstMetal(start:=TRUE);
        
      	// Transições de estado
      	IF posicionarEstMetal.done THEN
          posicionarEstMetal(start := FALSE);
      		state := 50;
      	END_IF;
        

    50: // Return to idle position
        empCNC(targetX := constante.Xidle, targetY := constante.Yidle, targetZ := constante.Zidle, start := TRUE);
        IF empCNC.done THEN
            empCNC(start := FALSE);
            state := 60;
        END_IF

    60: // Finalizado
        done := TRUE;
		state := 0;

END_CASE
